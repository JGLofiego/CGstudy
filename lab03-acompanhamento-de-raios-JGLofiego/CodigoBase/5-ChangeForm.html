<html>
	<head>
		<meta http-equiv="content-type" content="text/html; charset=UTF-8">
		<meta name="viewport" content="width=device-width, initial-scale=1.0, user-scalable=yes">
		<link rel="icon" type="image/x-icon" href="/Assets/icons/icon.png">	
	</head>

	<!-- Import maps polyfill -->
	<!-- Remove this when import maps will be widely supported -->
	<script async src="https://unpkg.com/es-module-shims@1.3.6/dist/es-module-shims.js"></script>

	<script type="importmap">
		{
			"imports": {
				"three": "/Assets/scripts/three.js/build/three.module.js"
			}
		}
	</script>

	<script id="RayTracing_VS" type="x-shader/x-vertex">

		varying vec3 vWorld; 	
	
		void main(void) {
			vWorld = (modelMatrix * vec4(position, 1.0)).xyz;
			gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1.0);
		}
	</script>
	
	<script id="RayTracing_FS" type="x-shader/x-fragment">
		precision mediump float;

		uniform vec3 uCamPos;
		uniform vec4 sphereColor;
		uniform float form, refraction_index;
		uniform bool enable_refraction;

		varying vec3 vWorld; 

		vec3 eCenter 	= vec3(0.0, 0.0, 0.0);
		vec3 	O, 
				P;

		float eRadius 	= 8.0;
		vec3  aColor 	= vec3(1.0, 0.0, 0.0);
		vec3 nPlane     = vec3(0.0, 0.0, 1.0);
		float tPlane;

		/// *********************************************
		float rayHitSphere(vec3 ray) {
			float a = dot(ray, ray);
			float b = 2.0 * dot(O, ray);
			float c = dot (O, O) - eRadius*eRadius;
			float delta = b*b - (4.0*a*c);

			if (delta < 0.0)
				return -1.0;
			
			return  min( ((-b) - sqrt(delta)) / (2.0*a), 
						 ((-b) + sqrt(delta)) / (2.0*a) );
		}
		
		float rayHitCylinder(vec3 ray) {

			float a = ray.x*ray.x + ray.y*ray.y;
			float b = 2.0 * (O.x * ray.x + O.y * ray.y);
			float c = O.x*O.x + O.y*O.y - eRadius*eRadius;
			float delta = b*b - (4.0*a*c);

			if (delta < 0.0)
				return -1.0;

			float t1 = ((-b) - sqrt(delta)) / (2.0*a);
			float t2 = ((-b) + sqrt(delta)) / (2.0*a);

			float z1 = O.z + ray.z * t1;
			float z2 = O.z + ray.z * t2;

			if((z1 < 0.0 || z1 > 5.0) && (z2 < 0.0 || z2 > 5.0)){
				return -1.0;
			} else {
				return  min(t1, t2);
			}
		}

		float rayHitCone(vec3 ray) {

			float a = ray.x*ray.x + ray.y*ray.y - ray.z*ray.z;
			float b = 2.0 * (O.x * ray.x + O.y * ray.y - O.z * ray.z);
			float c = O.x*O.x + O.y*O.y - O.z*O.z;
			float delta = b*b - (4.0*a*c);

			if (delta < 0.0)
				return -1.0;

			float t1 = ((-b) - sqrt(delta)) / (2.0*a);
			float t2 = ((-b) + sqrt(delta)) / (2.0*a);

			float z1 = O.z + ray.z * t1;
			float z2 = O.z + ray.z * t2;

			if((z1 < 0.0 || z1 > 5.0) && (z2 < 0.0 || z2 > 5.0)){
				return -1.0;
			} else {
				return  min(t1, t2);
			}
		}
			
		/// *********************************************
		vec3 chessBoard(vec3 point){
			float valueX = mod(point.x, 2.0);
			float valueY = mod(point.y, 2.0);

			if(valueX > -1.0 && valueX < 1.0){
				if(valueY < 1.0 && valueY > -1.0){
					return vec3(1,1,1);
				} else {
					return vec3(0,0,0);
				}
			} else if(valueX <= -1.0 || valueX >= 1.0) {
				if(valueY < 1.0 && valueY > -1.0){
					return vec3(0,0,0);
				} else {
					return vec3(1,1,1);
				}
			}
		}
		
		vec4 CorDeFundo(vec3 ray) {
			return vec4(chessBoard(ray), 1.0);
		}
		
		float intersectionP(vec3 ray, vec3 origin, vec3 pPlane, vec3 normal) {

			float t = dot((pPlane - origin), normal) / dot(ray, normal);

			return t;
		}

		void main(void) {	

			O = uCamPos - eCenter;
			P = vWorld - eCenter;

			vec3 point;

			vec3 D = P - O;

			float t;

			if(form == 1.0){
				t = rayHitSphere(D);
			} else if(form == 2.0){
				t = rayHitCylinder(D);
			} else if(form == 3.0){
				t = rayHitCone(D);
			}
			
			if (t == -1.0) 
				gl_FragColor = CorDeFundo(D);
			else {

				if(enable_refraction){
					vec3 pointS = O + D * t;
					
					vec3 dN = normalize(D);

					vec3 n;
					
					if(form == 1.0){
						n = normalize(eCenter - pointS);
					} else if(form == 2.0){
						n = normalize(vec3(0,0, pointS.z) - pointS);
					} else if(form == 3.0){
						float zRadius = sqrt(pointS.x*pointS.x + pointS.y*pointS.y);
						n = normalize(vec3(pointS.x/zRadius, pointS.y/zRadius, 0.0));
					}
	
					vec3 refractionD = refract(dN, n, refraction_index);
					float refractionT = intersectionP(refractionD, pointS, vec3(0.0, 0.0, 5.0), nPlane);
	
					point = pointS + refractionT * refractionD;
				} else {
					point = D;
				}
				
				gl_FragColor = vec4(sphereColor.w) * sphereColor + vec4(1.0 - sphereColor.w) * CorDeFundo(point);
				
			}
		}
				
			</script>

	<script type=module src="./cenarioBaseRT.js"></script>
	
	<body>
		<h3 id="output-text">Lan√ßando Raios em uma Esfera</h3>
		<div id="threejs-canvas"></div>
	</body>
</html>
	</body>
</html>